repositories {
    mavenLocal()
    mavenCentral()
    maven { url = 'https://repo.grails.org/grails/core' }
}

configurations.configureEach {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if ((details.requested.group == 'org.codehaus.groovy' || details.requested.group == 'org.apache.groovy') && details.requested.name != 'groovy-bom') {
            String groovyVersion = findProperty('groovyVersion') ?: buildsrcLibs.versions.groovy.get()
            details.useTarget(group: 'org.apache.groovy', name: details.requested.name, version: groovyVersion)
            details.because "The dependency coordinates are changed in Apache Groovy 4, plus ensure version"
        }
    }
}

dependencies {
    implementation buildsrcLibs.asciidoctorj
    implementation buildsrcLibs.asset.pipeline.gradle
    implementation buildsrcLibs.grails.gradle.plugin, {
        // Grails Gradle plugin leaks groovy-xml onto compile classpath
        // which is causes a version conflict for Gradle
        exclude group: 'org.apache.groovy', module: 'groovy-xml'
    }
    implementation buildsrcLibs.webdriver.binaries.gradle.plugin

    testImplementation buildsrcLibs.spock.core
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}
